plugins {
    alias libs.plugins.android.library
    alias libs.plugins.jetbrains.kotlin.android
    alias libs.plugins.ksp
    id 'maven-publish'
}

android {
    namespace 'org.koitharu.workinspector'
    compileSdk 35

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-opt-in=kotlin.ExperimentalStdlibApi',
                '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-opt-in=kotlinx.coroutines.FlowPreview',
                '-Xexplicit-api=strict'
        ]
    }
    libraryVariants.configureEach { variant ->
        variant.addJavaSourceFoldersToModel(file("build/generated/ksp/${variant.name}/kotlin"))
    }
    lint {
        abortOnError true
        disable 'RestrictedApi'
    }
}

dependencies {

    implementation libs.kotlinx.coroutines

    implementation libs.androidx.core
    implementation libs.androidx.activity
    implementation libs.material
    implementation libs.androidx.constraintlayout

    implementation platform(libs.koin.bom)
    implementation libs.koin.android
    implementation libs.koin.coroutines
    implementation libs.koin.annotations
    ksp libs.koin.compiler

    implementation libs.work
    implementation libs.room
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.koitharu'
                artifactId = 'work-inspector'
                version = '1.0'
            }
        }
    }
}